# Testing GenServers

```elixir
Mix.install([
  {:kino, "~> 0.7.0", override: true},
  {:youtube, github: "brooklinjazz/youtube"},
  {:hidden_cell, github: "brooklinjazz/hidden_cell"}
])
```

## Navigation

[Return Home](../start.livemd)<span style="padding: 0 30px"></span>
[Report An Issue](https://github.com/DockYard-Academy/beta_curriculum/issues/new?assignees=&labels=&template=issue.md&title=)

## Setup

Ensure you type the `ea` keyboard shortcut to evaluate all Elixir cells before starting. Alternatively you can evaluate the Elixir cells as you read.

## Testing GenServers

We've seen how to perform tests on a module and function, but how do you test something stateful like a process?
Let's consider how we could test a simple `Counter` process that should store an integer in it's state and increment the value.

```elixir
defmodule Counter do
  use GenServer

  @impl true
  def init(state) do
    {:ok, state}
  end

  @impl true
  def handle_call(:increment, _from, state) do
    {:reply, state + 1, state + 1}
  end

  @impl true
  def handle_call(:get_count, _from, state) do
    {:reply, state, state}
  end
end
```

Generally, we don't want to test the implementation of the counter, so we don't
want to specifically send it messages using the [GenServer](https://hexdocs.pm/elixir/GenServer.html) module, nor do we want
to test on the state of the process.

For example, the following test is coupled to implementation.

```elixir
ExUnit.start(auto_run: false)

defmodule CounterTest do
  use ExUnit.Case

  test "Counter receives :increment call" do
    {:ok, pid} = GenServer.start_link(Counter, 0)
    GenServer.call(pid, :increment)
    assert :sys.get_state(pid) == 1
  end
end

ExUnit.run()
```

So if any of the internals change, these tests could break, even though the behavior of the counter module doesn't.
Instead, we generally want to test on the [client interface](https://hexdocs.pm/elixir/GenServer.html#module-client-server-apis) of the [GenServer](https://hexdocs.pm/elixir/GenServer.html).

Here's the `Counter` module separated into the `Client` and `Server` interface.

```elixir
defmodule Counter do
  use GenServer

  # Client

  def start_link(_opts) do
    GenServer.start_link(__MODULE__, 0)
  end

  def increment(pid) do
    GenServer.call(pid, :increment)
  end

  def get_count(pid) do
    GenServer.call(pid, :get_count)
  end

  # Server

  @impl true
  def init(state) do
    {:ok, state}
  end

  @impl true
  def handle_call(:get_count, _from, state) do
    {:reply, state, state}
  end

  @impl true
  def handle_call(:increment, _from, state) do
    {:reply, state, state + 1}
  end
end
```

Now we can use these functions in our test.

```elixir
ExUnit.start(auto_run: false)

defmodule CounterTest do
  use ExUnit.Case

  test "increment/1" do
    {:ok, pid} = Counter.start_link([])
    Counter.increment(pid)
    assert Counter.get_count(pid) == 1
  end
end

ExUnit.run()
```

Now if any of the internals of `Counter` change, so long as the client interface and behavior remain the same the tests will not break.
For example, let's say we decide to change how we store state in the `Counter`. Instead of an integer, it will be a keyword list like so.

```elixir
defmodule Counter do
  use GenServer

  # Client

  def start_link(_opts) do
    GenServer.start_link(__MODULE__, %{count: 0})
  end

  def increment(pid) do
    GenServer.call(pid, :increment)
  end

  def get_count(pid) do
    GenServer.call(pid, :get_count)
  end

  # Server

  @impl true
  def init(state) do
    {:ok, state}
  end

  @impl true
  def handle_call(:get_count, _from, state) do
    {:reply, state.count, state}
  end

  @impl true
  def handle_call(:increment, _from, state) do
    {:reply, state.count + 1, %{state | count: state.count + 1}}
  end
end
```

Our tests still pass, because the behavior and interface has not changed.

```elixir
ExUnit.start(auto_run: false)

defmodule CounterTest do
  use ExUnit.Case

  test "increment/1" do
    {:ok, pid} = Counter.start_link([])
    Counter.increment(pid)
    assert Counter.get_count(pid) == 1
  end
end

ExUnit.run()
```

However, the old test suite breaks, because it was coupled to the underlying implementation.

```elixir
ExUnit.start(auto_run: false)

defmodule CounterTest do
  use ExUnit.Case

  test "handle_call :increment" do
    {:ok, pid} = GenServer.start_link(Counter, 0)
    GenServer.call(pid, :increment)
    assert :sys.get_state(pid) == 1
  end
end

ExUnit.run()
```

For more on testing [GenServer](https://hexdocs.pm/elixir/GenServer.html)s, there is an excellent talk by Tyler Young.

<!-- livebook:{"attrs":{"source":"YouTube.new(\"https://www.youtube.com/watch?v=EZFLPG7V7RM\")","title":"Testing GenServers"},"kind":"Elixir.HiddenCell","livebook_object":"smart_cell"} -->

```elixir
YouTube.new("https://www.youtube.com/watch?v=EZFLPG7V7RM")
```

### Your Turn

Implement and test a `decrement/1` function on the `Counter` module.

```elixir
defmodule Counter do
  use GenServer

  # Client

  def start_link(_opts) do
    GenServer.start_link(__MODULE__, %{count: 0})
  end

  def increment(pid) do
    GenServer.call(pid, :increment)
  end

  def get_count(pid) do
    GenServer.call(pid, :get_count)
  end

  def decrement(pid) do
    # Implementation
  end

  # Server

  @impl true
  def init(state) do
    {:ok, state}
  end

  @impl true
  def handle_call(:get_count, _from, state) do
    {:reply, state.count, state}
  end

  @impl true
  def handle_call(:increment, _from, state) do
    {:reply, state.count + 1, %{state | count: state.count + 1}}
  end

  @impl true
  def handle_call(:decrement, _from, state) do
    # Implementation
  end
end

ExUnit.start(auto_run: false)

defmodule CounterTest do
  use ExUnit.Case

  test "increment/1" do
    {:ok, pid} = Counter.start_link([])
    Counter.increment(pid)
    assert Counter.get_count(pid) == 1
  end

  test "decrement/1"
end

ExUnit.run()
```

## Commit Your Progress

Run the following in your command line from the curriculum folder to track and save your progress in a Git commit.
Ensure that you do not already have undesired or unrelated changes by running `git status` or by checking the source control tab in Visual Studio Code.

```
$ git checkout main
$ git checkout -b exercise-testing_genservers
$ git add .
$ git commit -m "finish testing genservers section"
$ git push origin exercise-testing_genservers
```

Create a pull request to your forked `main` branch. Please do not create a pull request to the DockYard Academy repository as this will spam our PR tracker.

**DockYard Academy Students Only:**

Notify your teacher by including `@BrooklinJazz` in your PR description to get feedback.

If you are interested in joining the next academy cohort, [sign up here](https://academy.dockyard.com/) to receive more news when it is available.

## Up Next

| Previous                                 | Next                               |
| ---------------------------------------- | ---------------------------------: |
| [Dominoes](../exercises/dominoes.livemd) | [Stack](../exercises/stack.livemd) |
