# Pokemon Battle

```elixir
Mix.install([
  {:jason, "~> 1.4"},
  {:kino, "~> 0.9", override: true},
  {:youtube, github: "brooklinjazz/youtube"},
  {:hidden_cell, github: "brooklinjazz/hidden_cell"}
])
```

## Navigation

<div style="display: flex; align-items: center; width: 100%; justify-content: space-between; font-size: 1rem; color: #61758a; background-color: #f0f5f9; height: 4rem; padding: 0 1rem; border-radius: 1rem;">
<div style="display: flex;">
<i class="ri-home-fill"></i>
<a style="display: flex; color: #61758a; margin-left: 1rem;" href="../start.livemd">Home</a>
</div>
<div style="display: flex;">
<i class="ri-bug-fill"></i>
<a style="display: flex; color: #61758a; margin-left: 1rem;" href="https://github.com/DockYard-Academy/curriculum/issues/new?assignees=&labels=&template=issue.md&title=Pokemon Battle">Report An Issue</a>
</div>
<div style="display: flex;">
<i class="ri-arrow-left-fill"></i>
<a style="display: flex; color: #61758a; margin-left: 1rem;" href="../exercises/rpg_dialogue.livemd">RPG Dialogue</a>
</div>
<div style="display: flex;">
<a style="display: flex; color: #61758a; margin-right: 1rem;" href="../reading/ranges.livemd">Ranges</a>
<i class="ri-arrow-right-fill"></i>
</div>
</div>

## Overview

You're going to create a pokemon battle game. If you're not familiar with pokemon, it's a game where elemental creatures battle.

You're going to represent pokemon using a `Pokemon` struct with the keys `:name`, `:type`, `:health`, `:attack`, and `:speed`. Enforce that all keys must have a value.

* `:name` is a string.
* `:type` will be an atom of either `:grass`, `:water`, or `:fire`
* `:health` will be an integer with a default value of `20`
* `:attack` will be an integer with a default value of `5`
* `:speed` will be an integer

<div style="padding: 1rem; margin: 1rem 0; background-color: lightgreen; font-weight: bold;">
You do not need to validate the data types for the keys above.
These are simply the expected data types for each `Pokemon` struct instance.
</div>

<!-- livebook:{"force_markdown":true} -->

```elixir
%Pokemon{name: "Bulbasaur", type: :grass, health: 20, attack: 5, speed: 20}
```

Then implement the `Pokemon.attack/2` function according to the documented examples.

<!-- livebook:{"force_markdown":true} -->

```elixir
attacker = %Pokemon{name: "Bulbasaur", type: :grass, health: 20, attack: 5, speed: 20}
defender = %Pokemon{name: "Bulbasaur", type: :grass, health: 20, attack: 5, speed: 20}

Pokemon.attack(attacker, defender)
# Output: Subtract Attack From Health To Return An Updated Pokemon Struct
%Pokemon{name: "Bulbasaur", type: :grass, health: 15, attack: 5, speed: 20}
```

`Pokemon.attack/2` should apply double damage if the pokemon has a type advantage, and half damage if the pokemon has a type disadvantage.

The advantages are:

* `:fire` is strong against `:grass`
* `:grass` is strong against `:water`
* `:water` is strong against `:fire`
* `:fire` is weak against `:water`
* `:grass` is weak against `:fire`
* `:water` is weak against `:grass`
* the same type should deal normal damage.

<!-- livebook:{"force_markdown":true} -->

```elixir
attacker = %Pokemon{name: "Charmander", type: :fire, health: 20, attack: 5, speed: 20}
defender = %Pokemon{name: "Bulbasaur", type: :grass, health: 20, attack: 5, speed: 20}

Pokemon.attack(attacker, defender)
# Output: Subtract Attack (doubled) From Health To Return An Updated Pokemon Struct
%Pokemon{name: "Bulbasaur", type: :grass, health: 10, attack: 5, speed: 20}
```

```elixir
defmodule Pokemon do
  @moduledoc """
  Pokemon

  ## Examples

    iex> %Pokemon{name: "Charmander", type: :fire, health: 20, attack: 5, speed: 20}
    %Pokemon{name: "Charmander", type: :fire, health: 20, attack: 5, speed: 20}

    iex> %Pokemon{name: "Charmander", type: :fire, speed: 20}
    %Pokemon{name: "Charmander", type: :fire, health: 20, attack: 5, speed: 20}
  """
  @enforce_keys [:name, :type, :speed]
  defstruct @enforce_keys ++ [health: 20, attack: 5]

  @doc """
  subtract the attack damage of an attacker from the defender's health.
  return an updated pokemon struct, and take type advantage into consideration.

  ## Examples

    Same Type

    iex> attacker = %Pokemon{name: "Charmander", type: :fire, health: 20, attack: 5, speed: 20}
    iex> defender = %Pokemon{name: "Charmander", type: :fire, health: 20, attack: 5, speed: 20}
    iex> Pokemon.attack(attacker, defender)
    %Pokemon{name: "Charmander", type: :fire, health: 15, attack: 5, speed: 20}

    Type Disadvantage

    iex> attacker = %Pokemon{name: "Bulbasaur", type: :grass, health: 20, attack: 5, speed: 20}
    iex> defender = %Pokemon{name: "Charmander", type: :fire, health: 20, attack: 5, speed: 20}
    iex> Pokemon.attack(attacker, defender)
    %Pokemon{name: "Charmander", type: :fire, health: 17.5, attack: 5, speed: 20}

    Type Advantage

    iex> attacker = %Pokemon{name: "Charmander", type: :fire, health: 20, attack: 5, speed: 20}
    iex> defender = %Pokemon{name: "Bulbasaur", type: :grass, health: 20, attack: 5, speed: 20}
    iex> Pokemon.attack(attacker, defender)
    %Pokemon{name: "Bulbasaur", type: :grass, health: 10, attack: 5, speed: 20}
  """
  @type_advantage %{fire: :grass, grass: :water, water: :fire}
  def attack(attacker, defender) do
    damage =
      cond do
        defender.type == @type_advantage[attacker.type] -> attacker.attack * 2
        attacker.type == @type_advantage[defender.type] -> attacker.attack / 2
        attacker.type == defender.type -> attacker.attack
      end

    %{defender | health: defender.health - damage}
  end
end
```

Consider using the following Elixir cell to test your solution.

```elixir
attacker = %Pokemon{name: "Bulbasaur", type: :grass, health: 20, attack: 5, speed: 20}
defender = %Pokemon{name: "Bulbasaur", type: :grass, health: 20, attack: 5, speed: 20}

Pokemon.attack(attacker, defender)
```

### Pokemon Instances

Bind three variables `charmander`, `bulbasaur`, and `squirtle` to an instance of a `Pokemon` struct with the following information.

```mermaid
classDiagram
  class charmander {
    name: "Charmander"
    type: :fire
    health: 20
    attack: 5
    speed: 20
  }
  class bulbasaur {
    name: "Bulbasar"
    type: :grass
    health: 20
    attack: 5
    speed: 20
  }
  class squirtle {
    name: "Squirtle"
    type: :water
    health: 20
    attack: 5
    speed: 20
  }
```

Use may these three structs to test your `Pokemon.attack/2` function.

<details style="background-color: lightgreen; padding: 1rem; margin: 1rem 0;">
<summary>Example solution</summary>

```elixir
charmander = %Pokemon{name: "Charmander", type: :fire, health: 20, attack: 5, speed: 20}
bulbasaur = %Pokemon{name: "Bulbasaur", type: :grass, health: 20, attack: 5, speed: 20}
squirtle = %Pokemon{name: "Squirtle", type: :water, health: 20, attack: 5, speed: 20}

# Testing Example
Pokemon.attack(charmander, bulbasaur)
```

</details>

Enter your solution below.

```elixir
charmander = %Pokemon{name: "Charmander", type: :fire, health: 20, attack: 5, speed: 20}
bulbasaur = %Pokemon{name: "Bulbasaur", type: :grass, health: 20, attack: 5, speed: 20}
squirtle = %Pokemon{name: "Squirtle", type: :water, health: 20, attack: 5, speed: 20}

Pokemon.attack(squirtle, charmander)
```

## Commit Your Progress

DockYard Academy now recommends you use the latest [Release](https://github.com/DockYard-Academy/curriculum/releases) rather than forking or cloning our repository.

Run `git status` to ensure there are no undesirable changes.
Then run the following in your command line from the `curriculum` folder to commit your progress.

```
$ git add .
$ git commit -m "finish Pokemon Battle exercise"
$ git push
```

We're proud to offer our open-source curriculum free of charge for anyone to learn from at their own pace.

We also offer a paid course where you can learn from an instructor alongside a cohort of your peers.
We will accept applications for the June-August 2023 cohort soon.

## Navigation

<div style="display: flex; align-items: center; width: 100%; justify-content: space-between; font-size: 1rem; color: #61758a; background-color: #f0f5f9; height: 4rem; padding: 0 1rem; border-radius: 1rem;">
<div style="display: flex;">
<i class="ri-home-fill"></i>
<a style="display: flex; color: #61758a; margin-left: 1rem;" href="../start.livemd">Home</a>
</div>
<div style="display: flex;">
<i class="ri-bug-fill"></i>
<a style="display: flex; color: #61758a; margin-left: 1rem;" href="https://github.com/DockYard-Academy/curriculum/issues/new?assignees=&labels=&template=issue.md&title=Pokemon Battle">Report An Issue</a>
</div>
<div style="display: flex;">
<i class="ri-arrow-left-fill"></i>
<a style="display: flex; color: #61758a; margin-left: 1rem;" href="../exercises/rpg_dialogue.livemd">RPG Dialogue</a>
</div>
<div style="display: flex;">
<a style="display: flex; color: #61758a; margin-right: 1rem;" href="../reading/ranges.livemd">Ranges</a>
<i class="ri-arrow-right-fill"></i>
</div>
</div>
