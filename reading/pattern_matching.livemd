# Pattern Matching

## Setup

```elixir
Mix.install([{:kino, github: "livebook-dev/kino"}])
```

## Pattern Matching

In many other programming languages, the match operator `=` is called an assignment operator.

That makes sense, because so far you've only seen the `=` used to bind values to variables.
However Elixir actually uses **pattern matching** and binding instead of assignment.

What is pattern matching? Well rather than simply assigning values to variables, Elixir allows
you to bind variables on the left-hand side of the `=`. So long as it matches the same shape of the
right hand side of the `=`.

What does that mean? Well, we already know we can use `=` to bind the right hand side value to a variable.

```elixir
my_tuple = {1, 2, 3}
```

But you can also bind a variable to each element in the tuple with pattern matching.

```elixir
{one, two, three} = {1, 2, 3}

two
```

So long as the expression on the left side of match operator `=` matches the pattern of the right side, 
you can bind variables to values.

Here's the same concept with a list

```elixir
[hero, secret_identity] = ["Spider Man", "Peter Parker"]

hero
```

This binds a new variable. If you aren't concerned about a value then you can use underscore `_`
to ignore it.

```elixir
[_, secret_identity] = ["Spider Man", "Peter Parker"]

secret_identity
```

However, if the left hand side doesn't match the right hand side, then the code will crash with 
a **MatchError**.

```elixir
[name] = ["Spider Man", "Peter Parker"]
```

You can pattern match with any kind of data. So long as the left side matches the right side.

### Your Turn

Replace the variable with a **pattern match** expression to bind every internal value to a new
variable. The first exercise is filled out as an example. Choose any variable name.

```elixir
%{spiderman: identity1} = %{spiderman: "Peter Parker"}
identity1
```

```elixir
variable = ["A"]
```

```elixir
variable = {"A", "B"}
```

```elixir
variable = [hello: "world"]
```

## Pattern Matching with Case

Previously, we talked about how the case statement checks for a match.

```elixir
case "same" do
  "same" -> "this will return"
end
```

We're also able to use pattern matching for case statements. The first valid match will execute,
and you can use the variables you assign in the instruction.

```elixir
case [1, 2] do
  [] -> nil
  [one] -> one
  [one, two] -> one + two
end
```

### Your turn

Trigger the `"A"`, `"B"`, `"C"`, and `"D"` cases by changing `my_variable`.

```elixir
my_variable = []

case my_variable do
  [] -> nil
  [1, 2, 3] -> "A"
  %{one: _} -> "B"
  {1, _, _, %{hello: _}} -> "C"
  _ -> "D"
end
```

## Pattern Matching Multi-Clause Functions

You've already learned about using multi-clause functions with different arity's or different guards.
You can also create multi-clause functions with pattern matching.

```elixir
defmodule Greeter do
  def greet([name1, name2]) do
    "Hello, #{name1} and #{name2}"
  end

  def greet(%{name: name, identity: identity}) do
    "Hi #{identity} err..I mean #{name}"
  end

  def greet(name) do
    "Hello, #{name}"
  end
end
```

```elixir
Greeter.greet("Peter")
```

```elixir
Greeter.greet(["Peter", "Bruce"])
```

```elixir
Greeter.greet(%{name: "Batman", identity: "Bruce Wayne"})
```

## Pattern Matching Lists

Sometimes a collection is large, and it's unreasonable to pattern match on every element.

Maps don't have this problem so much, because you don't need to include every key in the match

```elixir
%{key1: value} = %{key1: "value1", key2: "value2"}
value
```

This poses an issue for lists though who seem to require a match for every element.

```elixir
[one] = [1, 2]
```

To get around this, you can use the `[head | tail]` syntax for prepending elements to a list.

The **head** is the first element of the list.

```elixir
[head | _tail] = [1, 2, 3, 4, 5, 6, 7]
head
```

The **tail** is rest of the elements in the list.

```elixir
[_head | tail] = [1, 2, 3, 4, 5, 6, 7]
tail
```

You can access multiple elements at the start of the list separated by commas `,`.

```elixir
[one, two | _tail] = [1, 2, 3, 4, 5, 6, 7]
{one, two}
```

Why head and tail? That's because under the hood lists in Elixir are a [linked list](reading/linked_list_vs_tuples).
Essentially each element in the list knows the location of the next element in memory.

The **head** is the current element in the list, and the **tail** is the link to the remaining elements.

<!-- livebook:{"break_markdown":true} -->

```mermaid
flowchart LR
  subgraph Head
  L
  end
  subgraph Tail
  I
  S
  T
  end
  L -- tail --> I --> S --> T 
```

<!-- livebook:{"break_markdown":true} -->

### Your Turn

Pattern match on only the first element in this list using `[head | tail]` syntax.
Replace `nil` with your answer.

```elixir
[1, 2, 3]
```

## With

`with` is often used together with pattern matching to create "happy path" code.
It's useful whenever you have a series of cases or values that rely on eachother.

You can use with to create check some pre-conditions before executing instructions.

```mermaid
flowchart LR
  with --> 1
  1 --> 2
  2 --> 3
  3 --> 4
  1[pre-condition]
  2[pre-condition]
  3[pre-condition]
  4[instruction]
```

If any of the preconditions fail, the with statement will stop and return the value 
of the failed pre-condition.

```mermaid
flowchart LR
  1[pre-condition]
  2[pre-condition]
  3[pre-condition]
  4[instruction]
  with --> 1
  1 --> 2
  2 --> 3
  3 --> 4
  1 --> 5[failed pre-condition]
  2 --> 5
  3 --> 5
```

Alternatively, you can use `else` to handle the result of a failed precondition.

```mermaid
flowchart LR
  1[pre-condition]
  2[pre-condition]
  3[pre-condition]
  4[instruction]
  with --> 1
  1 --> 2
  2 --> 3
  3 --> 4
  1 --> 5[failed pre-condition]
  2 --> 5
  3 --> 5
  5 --> 6[else]
```

<!-- livebook:{"break_markdown":true} -->

Here's a minimal example `with` with a single pre-condition.

```elixir
is_admin = true

with true <- is_admin do
  "delete user"
end
```

The `with` statement checks is_admin. if `true`, it returns `"delete_user"`.
if any other value, it returns the value of is_admin.

```mermaid
flowchart LR
  with --> is_admin --> 3[delete user]
  is_admin --> 4[is_admin]
```

<!-- livebook:{"break_markdown":true} -->

`with` uses pattern maching to check if the left side of the `<-` matches the right side.
The example above is probably better served using a simple `if` statement, so let's make the example
more realistic and store `is_admin` in a boolean on a user map.

```elixir
user = %{is_admin: true}

with true <- user do
  "delete user"
end
```

Because `%{is_admin: true}` does not match `true`, the with statement returns `%{is_admin: true}`.
Let's correct that.

```elixir
user = %{is_admin: true}

with %{is_admin: true} <- user do
  "delete user"
end
```

Great! That's working. But this is still probably better handled by an `if` or `case` statement.

```elixir
user = %{is_admin: true}

if user === %{is_admin: true} do
  "delete user"
end
```

With is specifically really good for checking a series of pre-conditions.
Let's change our example to sending an email. To send an email we need to ensure:

* the sending user is an admin
* the recieving user has an email
* the email has a title and a body

We also need the `name` of the sender and reciever, and their emails.
If we were to use `case`, here's what that might look like. It's an unclear mess!

```elixir
sending_user = %{name: "Batman", email: "notbrucewayne@bat.net", is_admin: true}
recieving_user = %{name: "Robin", email: "boywonder@bat.net"}
email = %{title: "ROBIN!", body: "WE'RE OUT OF BAT SNACKS!"}

case sending_user do
  %{is_admin: true, name: sender_name, email: sender_email} ->
    case recieving_user do
      %{name: reciever_name, email: reciever_email} ->
        case email do
          %{title: title, body: body} ->
            "from #{sender_name}:#{sender_email} to #{reciever_name}:#{reciever_email} #{title}, #{body}"
        end
    end
end
```

Here's the same code rewritten using `with`. There's still some natural complexity, but it's 
a fair bit more clear.

```elixir
sending_user = %{name: "Batman", email: "notbrucewayne@bat.net", is_admin: true}
recieving_user = %{name: "Robin", email: "boywonder@bat.net"}
email = %{title: "ROBIN!", body: "WE'RE OUT OF BAT SNACKS!"}

with %{is_admin: true, name: sender_name, email: sender_email} <- sending_user,
     %{name: reciever_name, email: reciever_email} <- recieving_user,
     %{title: title, body: body} <- email do
  "from #{sender_name}:#{sender_email} to #{reciever_name}:#{reciever_email} #{title}, #{body}"
end
```

Right now, if a value doesn't match the precondition, it returns the value. for example,
if the sender is `nil` we return `nil`

```elixir
sending_user = nil
recieving_user = %{name: "Robin", email: "boywonder@bat.net"}
email = %{title: "ROBIN!", body: "WE'RE OUT OF BAT SNACKS!"}

with %{is_admin: true, name: sender_name, email: sender_email} <- sending_user,
     %{name: reciever_name, email: reciever_email} <- recieving_user,
     %{title: title, body: body} <- email do
  "from #{sender_name}:#{sender_email} to #{reciever_name}:#{reciever_email} #{title}, #{body}"
end
```

Sometimes it's useful to simply return the value, other times it's useful to handle the error. 
When we want to handle the error you can use `else`. The else block works like a `case`
statement here were we match on the return value of the pre-condition.

```elixir
sending_user = "batman"
recieving_user = %{name: "Robin", email: "boywonder@bat.net"}
email = %{title: "ROBIN!", body: "WE'RE OUT OF BAT SNACKS!"}

with %{is_admin: true, name: sender_name, email: sender_email} <- sending_user,
     %{name: reciever_name, email: reciever_email} <- recieving_user,
     %{title: title, body: body} <- email do
  "from #{sender_name}:#{sender_email} to #{reciever_name}:#{reciever_email} #{title}, #{body}"
else
  error -> "Email not sent because #{error} did not match expected format"
end
```

Much like a `case` statement, you can match on multiple cases to handle different errors.

```elixir
sending_user = %{name: "Joker", email: "joker@jokesonyou.haha"}
recieving_user = %{name: "Robin", email: "boywonder@bat.net"}
email = %{title: "ROBIN!", body: "WE'RE OUT OF BAT SNACKS!"}

with %{is_admin: true, name: sender_name, email: sender_email} <- sending_user,
     %{name: reciever_name, email: reciever_email} <- recieving_user,
     %{title: title, body: body} <- email do
  "from #{sender_name}:#{sender_email} to #{reciever_name}:#{reciever_email} #{title}, #{body}"
else
  %{name: "Joker"} -> "Get out of here Joker!"
  error -> "Email not sent because #{error} did not match expected format"
end
```

### Your turn

In the Elixir cell below

* create a variable scores that is a list `[5, 10, 20]`
* create a with statement that checks that `scores` has 3 elements and through pattern matching
  binds each to a variable.
* in the body of the with statement, add each number together.

```elixir

```

## Pin Operator

The **pin operator** allows us to use variables as hard-coded values, 
rather than rebinding a variable.

Often we use the pin operator when testing our code to assert that the value is correct.

For example, the following will rebind the **recieved** variable to `[1, 2, 3]`

```elixir
recieved = [1, 2]
expected = [1, 2, 3]

recieved = expected
```

But instead, we might use the match operator to check that the recieved value matches the expected
value.

```elixir
recieved = [1, 2]
expected = [1, 2, 3]

^recieved = expected
```

By using the pin operator above, we accomplish the same as if we had wrote:

```elixir
[1, 2] = [1, 2, 3]
```

We can also use this for internal values in a collection. The following is the same
as `[1, 2, 3] = [2, 2, 3]`

```elixir
first = 1
actual = [2, 2, 3]
[^first, 2, 3] = actual
```

### Your Turn

<!-- livebook:{"break_markdown":true} -->

In the Elixir cell below, use the pin operator to avoid rebinding a variable and instead check
that one value matches a pattern.

```elixir

```
